agent_gen_el
<- abstract_sc_agent;
=> nrel_primary_initiation_condition:
    (sc_event_after_generate_outgoing_arc => action_initiated);
=> nrel_sc_agent_action_class:
    action_gen_el;
=> nrel_initiation_condition_and_result:
    (..agent_gen_el_initiation_condition
        => ..agent_gen_el_result_condition);
<= nrel_sc_agent_key_sc_elements:
{
    action_initiated;
    action_gen_el
};
=> nrel_inclusion:
    agent_gen_el_implementation
    (*
        <- platform_independent_abstract_sc_agent;;
        <= nrel_sc_agent_program:
        {
            agent_gen_el_program
        };;
        -> agent_gen_el_active (* <- active_sc_agent;; *);;
    *);;

..agent_gen_el_initiation_condition
= [*
    action_gen_el _-> .._action;;
    action_initiated _-> .._action;;
    .._action _-> rrel_1:: .._set;;
    concept_set _-> .._set;;
*];;

..agent_gen_el_result_condition
= [*
    .._set _-> .._generated_element;;
*];;

action_gen_el
    <- sc_node_class;
    <= nrel_inclusion: information_action;;

agent_gen_el_program
<- agent_scp_program;
<- scp_program;
-> rrel_key_sc_element: _process1;;

agent_gen_el_program = [*
@input_arc = (..._ _-> ..._);;
_process1 
_<- scp_process;
//params
_-> rrel_1:: rrel_in:: _event;
_-> rrel_2:: rrel_in:: @input_arc;

//operators
_<= nrel_decomposition_of_action:: _... (*

        _-> rrel_1:: _..operator1
        (*
        _<- searchElStr3;;

        _-> rrel_1:: rrel_assign:: rrel_scp_var:: _temp;;
        _-> rrel_2:: rrel_fixed:: rrel_scp_const:: @input_arc;;
        _-> rrel_3:: rrel_assign:: rrel_scp_var:: _quest;;

        _=> nrel_then:: _..operator2;;
        _=> nrel_else:: _..operator_return;;
        *);;

        _-> _..operator2
        (*
        _<- genEl;;

        _-> rrel_1:: rrel_assign:: rrel_scp_var:: rrel_node:: rrel_const:: _generated_element;;

        _=> nrel_goto:: _..operator3;;
        *);;

        _-> _..operator3
        (*
        _<- searchElStr3;;

        _-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
        _-> rrel_2:: rrel_assign:: rrel_scp_var:: rrel_pos_const_perm:: _arc1;;
        _-> rrel_3:: rrel_assign:: rrel_scp_var:: rrel_node:: rrel_const:: _set;;

        _=> nrel_then:: _..operator4;;
        _=> nrel_else:: _..operator_return;;
        *);;

        _-> _..operator4
        (*
        _<- genElStr3;;

        _-> rrel_1:: rrel_fixed:: rrel_scp_var:: _set;;
        _-> rrel_2:: rrel_assign:: rrel_scp_var:: rrel_pos_const_perm:: _arc1;;
        _-> rrel_3:: rrel_fixed:: rrel_scp_var:: _generated_element;;

        _=> nrel_goto:: _..operator5;;
        *);;

        _-> _..operator5
        (*
        _<- genElStr3;;

        _-> rrel_1:: rrel_assign:: rrel_scp_var:: rrel_node:: rrel_const:: _result;;
        _-> rrel_2:: rrel_assign:: rrel_scp_var:: rrel_pos_const_perm:: _arc_from_result;;
        _-> rrel_3:: rrel_fixed:: rrel_scp_var:: _arc1;;

        _=> nrel_goto:: _..operator6;;
        *);;

        _-> _..operator6
        (*
        _<- genElStr3;;

        _-> rrel_1:: rrel_fixed:: _result;;
        _-> rrel_2:: rrel_assign:: rrel_scp_var:: rrel_pos_const_perm:: _arc_from_result;;
        _-> rrel_3:: rrel_fixed:: rrel_scp_var:: _set;;

        _=> nrel_goto:: _..operator7;;
        *);;

        _-> _..operator7
        (*
        _<- genElStr3;;

        _-> rrel_1:: rrel_fixed:: _result;;
        _-> rrel_2:: rrel_assign:: rrel_scp_var:: rrel_pos_const_perm:: _arc_from_result;;
        _-> rrel_3:: rrel_fixed:: rrel_scp_var:: _generated_element;;

        _=> nrel_goto:: _..operator8;;
        *);;


        _-> _..operator8
        (*
        _<- genElStr5;;

        _-> rrel_1:: rrel_fixed:: rrel_scp_var:: _quest;;
        _-> rrel_2:: rrel_assign:: rrel_scp_var:: rrel_const:: rrel_common:: rrel_arc:: _arc1;;
        _-> rrel_3:: rrel_fixed:: rrel_scp_var:: _result;;
        _-> rrel_4:: rrel_assign:: rrel_scp_var:: rrel_pos_const_perm:: _arc2;;
        _-> rrel_5:: rrel_fixed:: rrel_scp_const:: nrel_result;;

        _=> nrel_goto:: _..operator_return;;
        *);;

        _-> _..operator_return
        (*
        _<- return;;
        *);;

*);;

*];;


scp_operator_atomic_type
    <- sc_node_class;
    -> return;
    -> genElStr5;
    -> genElStr3;
    -> searchElStr3;
    -> genEl;
    -> searchSetStr5;;


order_role_relation
    <- sc_node_class;
    -> rrel_1;
    -> rrel_2;
    -> rrel_3;
    -> rrel_4;
    -> rrel_5;
    -> rrel_6;
    -> rrel_7;
    -> rrel_8;
    -> rrel_9;
    -> rrel_10;;
